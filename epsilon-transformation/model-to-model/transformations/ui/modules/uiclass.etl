import "../../../operations/_importAll.eol";

rule UIClass
	transform jclass : PSM!JClass
	to uiclass : UI!UIClass {
	guard : jclass.package.participates(PSM!JLayer#OPERATION) or jclass.package.participates(PSM!JLayer#SCREEN) 
	//inherited from Identifiable
	uiclass.uuid = jclass.uuid;
	uiclass.name = jclass.name;
	
	//Attributes
	uiclass.`abstract` = jclass.`abstract`;
	uiclass.enumClass = jclass.representsEnum;
	uiclass.singleton = jclass.businessSingleton;
	if (jclass.visibility = PSM!JVisibility#PROTECTED or jclass.visibility = PSM!JVisibility#PRIVATE or jclass.visibility = PSM!JVisibility#PACKAGE) {
		uiclass.readonly = true;
	} else {
		uiclass.readonly = false;
	}

	//References
	var representation : UI!UIBaseComponentType = jclass.representation.equivalent("UIBaseComponentType");
	if (representation.isDefined()) {
		representation.representation = true;
		uiclass.representation = representation;
	}
	
	uiclass.super = jclass.supertype.equivalent("UIClass");

	//create ComponentTypes/Actions
	
	for (attr : PSM!JAttribute in jclass.attributes) {
		uiClass.attributes.add(attr.equivalent("UIBaseComponentType"));
	}
	for (role : PSM!JRole in jclass.roles) {
		uiClass.attributes.add(role.equivalent("UIReferenceComponentType"));
	}
	for (op : PSM!JOperation in jclass.operations) {
		if (op.visibility = PSM!JVisibility#PUBLIC) {
			uiClass.actions.add(op.equivalent("UIAction"));
		}	
	}
	
	for (class : PSM!JClass in jclass.getSuperTypes()) {
		var superType = class.equivalent("UIClass");
		var inheretedAttributes = superType.attributes;
		var inheretedActions = superType.actions;
		
		uiClass.inheritedAttributes.addAll(inheretedAttributes);
		uiClass.inheritedActions.addAll(inheretedActions);
	}

	uiclass.~attributesForListing = new Sequence(UI!UIComponentType);
	for (attribute in jclass.attributesForListing) {
		uiclass.~attributesForListing.add(attribute.equivalent("UIBaseComponentType"));
	}

	//create Views
	var classView = createClassView(uiclass, jclass, uiclass.name, uiclass.uuid + '_oview_default', true);
	uiclass.selector = createListView(uiclass, uiclass.name, uiclass.uuid + '_lview_default', classView, true);
	uiclass.classViews.add(classView);
	uiclass.listViews.add(uiclass.selector);
	
	for (al in jclass.aliases) {
		classView = createClassView(uiclass, jclass, al.name, uiclass.uuid + '_oview_' + al.name, false);
		var listView =  createListView(uiclass, al.name, uiclass.uuid + '_lview_' + al.name, classView, false);
		uiclass.classViews.add(classView);
		uiclass.listViews.add(listView);
	}
}



