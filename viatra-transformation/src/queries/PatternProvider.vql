package queries

import "platform:/resource/psm_meta/src/model/psm.ecore"
import "platform:/resource/ui_meta/src/model/ui.ecore"
import "platform:/resource/ui_meta/src/model/psmToUiTypeMapping.ecore"
import "platform:/resource/traceability_meta/model/traceability.ecore"


//UIBase Queries
pattern UIBaseQuery(uiBase: UIBase) {
	UIBase(uiBase);
}

//JModel Queries
pattern domainJModelQuery(jModel : JModel) {
	JModel(jModel);
	JModel.framework(jModel, false);
}

//JUIFilter Queries
pattern JUIFilterQuery(jFilter : JUIFilter) {
	JUIFilter(jFilter);
}

//JPackage Queries
pattern JPackageToUIModuleQuery(jPackage : JPackage) {
	find notOnlyContainerPackage(jPackage);
	find findUIRelevantPackages(jPackage);	
}

private pattern findUIRelevantPackages(jPackage : JPackage) {
	find findPackagesInLayer(jPackage, JLayer::SCREEN);
} or {
	find findPackagesInLayer(jPackage, JLayer::OPERATION);
} or {
	find findPackagesInLayer(jPackage, JLayer::ALL);
}

private pattern findPackagesInLayer(jPackage : JPackage, layer : JLayer) {
	JPackage.participates(jPackage, layer);
}

private pattern notOnlyContainerPackage(jPackage : JPackage) {
	JPackage(jPackage);
	primitives == count find primitivesInPackage(jPackage, _);
	enumerations == count find enumerationsInPackage(jPackage, _);
	relationships == count find relationshipsInPackage(jPackage, _);
	classes == count find classesInPackage(jPackage, _);
	check (
		enumerations + primitives + relationships + classes > 0
	);
}

private pattern primitivesInPackage(jPackage : JPackage, jPrimitive : JPrimitive) {
	JPackage.primitives(jPackage, jPrimitive);
}

private pattern enumerationsInPackage(jPackage : JPackage, jEnumeration : JEnumeration) {
	JPackage.enumerations(jPackage, jEnumeration);
}

private pattern relationshipsInPackage(jPackage : JPackage, jRelationship : JRelationship) {
	JPackage.relationships(jPackage, jRelationship);
}

private pattern classesInPackage(jPackage : JPackage, jClass : JClass) {
	JPackage.classes(jPackage, jClass);
}


//UIClass Queries
pattern findDescendantsForClass(uiClass : UIClass, descendant : UIClass) {
	UIClass.^super+(descendant, uiClass);
}

pattern listViewForUIClass(uiClass : UIClass, view : UIListView) {
	UIClass(uiClass);
	UIClass.listView(uiClass, view);
}

pattern classViewForUIClass(uiClass : UIClass, view : UIClassView) {
	UIClass(uiClass);
	UIClass.classView(uiClass, view);
}


//JClass Queries
pattern JClassQuery(jClass : JClass) {
	JClass(jClass);
}

pattern JClassWithGuardConditionQuery(jClass : JClass) {
	JClass.^package(jClass, jPackage);
	find findUIRelevantPackages(jPackage);
}

pattern attributeGroupQuery(jClass : JClass, group : JUIAttributeGroup, attribute : JAttribute) {
	JClass.attributeOrder(jClass, group);
	JUIAttributeGroup.attributes(group, attribute);
}

pattern representsUserJClassQuery(jClass : JClass) {
	JClass.representsUser(jClass, true);
}

//UIACtion Queries
pattern resultViewForUIAction(action : UIAction, view : UIResultView) {
	UIAction(action);
	UIAction.resultView(action, view);
}

pattern paramViewForUIAction(action : UIAction, view : UIParamView) {
	UIAction(action);
	UIAction.paramView(action, view);
}

//JOperation Queries
pattern JOperationWithGuardQuery(jOperation : JOperation) {
	JOperation(jOperation);
	JOperation.visibility(jOperation, JVisibility::PUBLIC);
}

//JParameter Queries
pattern JParameterWithGuardQuery(jParameter : JParameter) {
	JParameter(jParameter);
	JOperation.parameters(jOperation, jParameter);
	JOperation.visibility(jOperation, JVisibility::PUBLIC);
}

pattern resultJParameter(jOperation : JOperation, jParameter : JParameter) {
	JOperation.parameters(jOperation, jParameter);
	JParameter.input(jParameter, false);
}

//UIMenuItem Queries
pattern mainMenuQuery(menuItem : UIMenuItem) {
	UIMenuItem.uuid(menuItem, "main_menu");
}

//JUIMenuItem Queries
pattern findMenuItemForFilter(jFilter : JUIFilter, jMenuItem : JUIMenuItem) {
	JUIMenuItem.uifilters(jMenuItem, jFilter);
}

pattern findMenuItemWithParent(jMenuItem : JUIMenuItem) {
	JUIMenuItem.parent(jMenuItem, _);
}

pattern menuRootChildren(menuItem : JUIMenuItem) {
	JModel.rootMenuItems(_JModel, Root);
	JUIMenuItem.children(Root, menuItem);
}

//JRole Queries
pattern JRoleQuery(jRole : JRole) {
	JRole(jRole);
}

//JAttribute Queries
pattern JAttributeQuery(jAttribute : JAttribute) {
	JAttribute(jAttribute);
}

//Trace Queries
pattern psmToUiTrace(jElement : JElement, identifiable : Identifiable) {
	PSMToUITrace.psmElements(trace, jElement);
	PSMToUITrace.uiElements(trace, identifiable);
}

//PSMToUIMapping Queries
pattern findCorrespondingType(jPrimitive : JPrimitive, componentType : UIComponentType) {
	Mapping.componentType(mapping, componentType);
	Mapping.psmType(mapping, jPrimitive);
}



