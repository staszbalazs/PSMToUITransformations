package queries

import "platform:/resource/psm_meta/src/model/psm.ecore"
import "platform:/resource/ui_meta/src/model/ui.ecore"
import "platform:/resource/ui_meta/src/model/psmToUiTypeMapping.ecore"
import "platform:/resource/traceability_meta/model/traceability.ecore"

//General
pattern hasUuid(jElement : JElement, uuid : java String) {
	JElement.uuid(jElement, uuid);
	check (
		uuid.length > 0
	);
}

//JModel Queries
pattern domainJModelQuery(jModel : JModel) {
	JModel.framework(jModel, false);
	find hasUuid(jModel, _);
	neg find alreadyTransformed(jModel, _, _);
}

pattern domainJModelQueryForModify(jModel : JModel) {
	find alreadyTransformed(jModel, _, _);
}

//JInfo Queries
pattern JInfoQuery(jInfo : JInfo) {
	find hasUuid(jInfo, _);
	neg find alreadyTransformed(jInfo, _, _);
}

pattern JInfoQueryForModify(jInfo : JInfo, uiInfo : UIInfo, trace : PSMToUITrace) {
	find alreadyTransformed(jInfo, uiInfo, trace);
}

//JUIFilter Queries
pattern JUIFilterQuery(jFilter : JUIFilter, uiMenuItem : UIMenuItem, componentType : UIBaseComponentType) {
	JUIMenuItem.uifilters(jMenuItem, jFilter);
	find alreadyTransformed(jMenuItem, uiMenuItem, _);
	
	JUIFilter.attribute(jFilter, attribute);
	find alreadyTransformed(attribute, componentType, _);
	
	find hasUuid(jFilter, _);
	JUIFilter.operator(jFilter, _);
	JUIFilter.value(jFilter, _);
	
	neg find alreadyTransformed(jFilter, _, _);	
}

pattern JUIFilterQueryForModify(jFilter : JUIFilter, uiFilter : UIFilter, uiMenuItem : UIMenuItem, componentType : UIBaseComponentType, trace : PSMToUITrace) {
	JUIMenuItem.uifilters(jMenuItem, jFilter);
	find alreadyTransformed(jMenuItem, uiMenuItem, _);
	
	JUIFilter.attribute(jFilter, attribute);
	find alreadyTransformed(attribute, componentType, _);
	
	find alreadyTransformed(jFilter, uiFilter, trace);
}

//JPackage Queries
pattern JPackageToUIModuleQuery(jPackage : JPackage) {
	find notOnlyContainerPackage(jPackage);
	find findUIRelevantPackages(jPackage);
	find hasUuid(jPackage, _);
	JPackage.name(jPackage, _);	
	neg find alreadyTransformed(jPackage, _, _);
}

pattern JPackageToUIModuleQueryForModify(jPackage : JPackage, uiModule : UIModule, trace : PSMToUITrace) {
	find alreadyTransformed(jPackage, uiModule, trace);
}

private pattern findUIRelevantPackages(jPackage : JPackage) {
	find findPackagesInLayer(jPackage, JLayer::SCREEN);
} or {
	find findPackagesInLayer(jPackage, JLayer::OPERATION);
} or {
	find findPackagesInLayer(jPackage, JLayer::ALL);
}

private pattern findPackagesInLayer(jPackage : JPackage, layer : JLayer) {
	JPackage.participates(jPackage, layer);
}

private pattern notOnlyContainerPackage(jPackage : JPackage) {
	primitives == count JPackage.primitives(jPackage, _);
	enumerations == count JPackage.enumerations(jPackage, _);
	relationships == count JPackage.relationships(jPackage, _);
	classes == count JPackage.classes(jPackage, _);
	check (
		enumerations + primitives + relationships + classes > 0
	);
}

//UIClass Queries
pattern findDescendantsForClass(uiClass : UIClass, descendant : UIClass) {
	UIClass.^super+(descendant, uiClass);
}

pattern findDescendantsForClassWithJClass(uiClass : UIClass, descendant : UIClass, jClass : JClass) {
	UIClass.^super+(descendant, uiClass);
	find alreadyTransformed(jClass, descendant, _);
}


//JClass Queries
pattern JClassWithGuardConditionQuery(jClass : JClass, uiModule : UIModule) {
	find hasUuid(jClass, _);
	JClass.^package(jClass, jPackage);
	find findUIRelevantPackages(jPackage);
	neg find alreadyTransformed(jClass, _, _);
	find alreadyTransformed(jPackage, uiModule, _);
}

pattern JClassWithGuardConditionQueryForModify(jClass : JClass, uiClass : UIClass, uiModule : UIModule, trace : PSMToUITrace) {
	JClass.^package(jClass, jPackage);
	find alreadyTransformed(jClass, uiClass, trace);
	find alreadyTransformed(jPackage, uiModule, _);
}

pattern representsUserJClassQuery(jClass : JClass) {
	JClass.representsUser(jClass, true);
}


//JOperation Queries
pattern JOperationWithGuardQuery(jOperation : JOperation, uiClass : UIClass) {
	find hasUuid(jOperation, _);
	JOperation.visibility(jOperation, JVisibility::PUBLIC);
	JOperation.ownerClass(jOperation, jClass);
	find alreadyTransformed(jClass, uiClass, _);
	neg find alreadyTransformed(jOperation, _, _);
}

pattern JOperationWithGuardQueryForModify(jOperation : JOperation, uiAction : UIAction, uiClass : UIClass, trace : PSMToUITrace) {
	JOperation.ownerClass(jOperation, jClass);
	find alreadyTransformed(jClass, uiClass, _);
	find alreadyTransformed(jOperation, uiAction, trace);
}

//JParameter Queries
pattern JParameterWithGuardQuery(jParameter : JParameter, uiAction : UIAction, uiType : Identifiable) {
	find hasUuid(jParameter, _);
	JOperation.parameters(jOperation, jParameter);
	JOperation.visibility(jOperation, JVisibility::PUBLIC);
	find alreadyTransformed(jOperation, uiAction, _);
	JParameter.type(jParameter, type);
	find alreadyTransformed(type, uiType, _);
	neg find alreadyTransformed(jParameter, _, _);
}

pattern JParameterWithGuardQueryForModify(jParameter : JParameter, parameterType : UIParameterComponentType, uiAction : UIAction, trace : PSMToUITrace, uiType : Identifiable) {
	JOperation.parameters(jOperation, jParameter);
	find alreadyTransformed(jOperation, uiAction, _);
	find alreadyTransformed(jParameter, parameterType, trace);
	JParameter.type(jParameter, type);
	find alreadyTransformed(type, uiType, _);
}

pattern JParameterViewFieldQuery(jParameter : JParameter, viewField : UIViewField, viewFieldSet : UIViewFieldSet, uiView : UIView, uiAction : UIAction) {
	find alreadyTransformed(jParameter, parameterType, _);
	UIViewField.componentType(viewField, parameterType);
	UIViewFieldSet.viewFields(viewFieldSet, viewField);
	UIView.viewFieldSets(uiView, viewFieldSet);
	JOperation.parameters(jOperation, jParameter);
	find alreadyTransformed(jOperation, uiAction, _);
}

//JUIMenuItem Queries
pattern findMenuItemWithParent(jMenuItem : JUIMenuItem) {
	find hasUuid(jMenuItem, _);
	JUIMenuItem.parent(jMenuItem, _);
	neg find alreadyTransformed(jMenuItem, _, _);
}

pattern findMenuItemToSetParent(jMenuItem : JUIMenuItem, uiMenuItem : UIMenuItem, parentMenuItem : UIMenuItem) {
	JUIMenuItem.parent(jMenuItem, parent);
	find alreadyTransformed(jMenuItem, uiMenuItem, _);
	find alreadyTransformed(parent, parentMenuItem, _);
}

pattern findMenuItemWithParentForModify(jMenuItem : JUIMenuItem, uiMenuItem : UIMenuItem, trace : PSMToUITrace) {
	find alreadyTransformed(jMenuItem, uiMenuItem, trace);
}

//JRole Queries
pattern JRoleQuery(jRole : JRole, opposite : JRole, uiClass : UIClass) {
	find hasUuid(jRole, _);
	find hasUuid(opposite, _);
	JRelationship.roles(relation, jRole);
	JRelationship.roles(relation, opposite);
	jRole != opposite;
	neg find alreadyTransformed(jRole, _, _);
	JRole.ownerClass(jRole, jClass);
	find alreadyTransformed(jClass, uiClass, _);
}

pattern JRoleQueryForModify(jRole : JRole, referenceComponentType : UIReferenceComponentType, oppositeReferenceType : UIReferenceComponentType, trace : PSMToUITrace) {
	find alreadyTransformed(jRole, referenceComponentType, trace);
	
	JRelationship.roles(relation, jRole);
	JRelationship.roles(relation, opposite);
	jRole != opposite;
	
	find alreadyTransformed(opposite, oppositeReferenceType, _);
}

pattern JRoleViewFieldQuery(jRole : JRole, referenceComponentType : UIReferenceComponentType, viewField : UIViewField, viewFieldSet : UIViewFieldSet, uiView : UIView, uiClass : UIClass) {
	find alreadyTransformed(jRole, referenceComponentType, _);
	JRole.ownerClass(jRole, jClass);
	find alreadyTransformed(jClass, uiClass, _);
	UIViewField.componentType(viewField, referenceComponentType);
	UIViewFieldSet.viewFields(viewFieldSet, viewField);
	UIView.viewFieldSets(uiView, viewFieldSet);
}

//JAttribute Queries
pattern JAttributeQuery(jAttribute : JAttribute, uiClass : UIClass, baseType : UIBaseComponentType) {
	find hasUuid(jAttribute, _);
	JClass.attributes(jClass, jAttribute);
	JAttribute.type(jAttribute, jPrimitive);
	find alreadyTransformed(jPrimitive, baseType, _);
	find alreadyTransformed(jClass, uiClass, _);
	neg find alreadyTransformed(jAttribute, _, _);
}

pattern JAttributeQueryForModify(jAttribute : JAttribute, uiClass : UIClass, componentType : UIBaseComponentType, baseType : UIBaseComponentType, trace : PSMToUITrace) {
	JClass.attributes(jClass, jAttribute);
	JAttribute.type(jAttribute, jPrimitive);
	find alreadyTransformed(jPrimitive, baseType, _);
	find alreadyTransformed(jClass, uiClass, _);
	find alreadyTransformed(jAttribute, componentType, trace);
}

pattern JAttributeInJGroupViewFieldQuery(jAttribute : JAttribute, viewFieldSet : UIViewFieldSet, uiBaseType : UIBaseComponentType, classView : UIClassView) {
	find alreadyTransformed(jAttribute, uiBaseType, _);
	JUIAttributeGroup.attributes(jGroup, jAttribute);
	find alreadyTransformed(jGroup, viewFieldSet, _);
	JClass.attributeOrder(jClass, jGroup);
	find alreadyTransformed(jClass, uiClass, _);
	UIClass.classView(uiClass, classView);
}

pattern JAttributeViewFieldQueryForModify(jAttribute : JAttribute, uiClass : UIClass, uiBaseType : UIBaseComponentType, viewField : UIViewField, viewFieldSet : UIViewFieldSet, uiView : UIView) {
	find alreadyTransformed(jAttribute, uiBaseType, _);
	JAttribute.ownerClass(jAttribute, jClass);
	find alreadyTransformed(jClass, uiClass, _);
	UIViewField.componentType(viewField, uiBaseType);
	UIViewFieldSet.viewFields(viewFieldSet, viewField);
	UIView.viewFieldSets(uiView, viewFieldSet);
}

//JUIAttributeGroup Queries
pattern JAttributeGroupQuery(jAttributeGroup : JUIAttributeGroup, classView : UIClassView) {
	find hasUuid(jAttributeGroup, _);
	JClass.attributeOrder(jClass, jAttributeGroup);
	find alreadyTransformed(jClass, uiClass, _);
	UIClass.classView(uiClass, classView);
	neg find alreadyTransformed(jAttributeGroup, _, _);
}

pattern JAttributeGroupQueryForModify(jAttributeGroup : JUIAttributeGroup, classView : UIClassView, viewFieldSet : UIViewFieldSet, trace : PSMToUITrace) {
	JClass.attributeOrder(jClass, jAttributeGroup);
	find alreadyTransformed(jClass, uiClass, _);
	UIClass.classView(uiClass, classView);
	find alreadyTransformed(jAttributeGroup, viewFieldSet, trace);
}


//Trace Queries
pattern psmToUiTrace(jElement : JElement, identifiable : Identifiable) {
	PSMToUITrace.psmElements(trace, jElement);
	PSMToUITrace.uiElements(trace, identifiable);
}

pattern alreadyTransformed(jElement : JElement, identifiable : Identifiable, trace : PSMToUITrace) {
	PSMToUITrace.psmElements(trace, jElement);
	PSMToUITrace.uiElements(trace, identifiable);
	Identifiable.uuid(identifiable, uuid);
	check (
		uuid.length > 0
	);
}

//PSMToUIMapping Queries
pattern findCorrespondingType(jPrimitive : JPrimitive, componentType : UIBaseComponentType) {
	Mapping.componentType(mapping, componentType);
	Mapping.psmType(mapping, jPrimitive);
}



