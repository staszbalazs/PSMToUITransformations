package queries

import "http://blackbelt/judo/meta/psm"
import "http://blackbelt/judo/meta/psm/ui"
import "http://blackbelt/judo/meta/psm/ui/traceability"
import "http://blackbelt/judo/meta/psm/ui/mapping"

//General
pattern hasUuid(jElement : JElement, uuid : java String) {
	JElement.uuid(jElement, uuid);
	check (
		uuid.length > 0
	);
}

//JModel Queries
pattern domainJModelQuery(jModel : JModel) {
	JModel.framework(jModel, false);
	find hasUuid(jModel, _);
	neg find alreadyTransformed(jModel, _, _);
}

pattern domainJModelQueryForModify(jModel : JModel) {
	find psmToUiTrace(jModel, _, _);
}

//JInfo Queries
pattern JInfoQuery(jInfo : JInfo) {
	find hasUuid(jInfo, _);
	neg find alreadyTransformed(jInfo, _, _);
}

pattern JInfoQueryForModify(jInfo : JInfo) {
	find alreadyTransformed(jInfo, _, _);
}

//JUIFilter Queries
pattern JUIFilterQuery(jFilter : JUIFilter, uiMenuItem : UIMenuItem, componentType : UIBaseComponentType) {
	JUIMenuItem.uifilters(jMenuItem, jFilter);
	find alreadyTransformed(jMenuItem, uiMenuItem, _);
	
	JUIFilter.attribute(jFilter, attribute);
	find alreadyTransformed(attribute, componentType, _);
	
	find hasUuid(jFilter, _);
	JUIFilter.operator(jFilter, _);
	JUIFilter.value(jFilter, _);
	
	neg find alreadyTransformed(jFilter, _, _);	
}

pattern JUIFilterQueryForModify(jFilter : JUIFilter, componentType : UIBaseComponentType, trace : PSMToUITrace) {
	find psmToUiTrace(jFilter, _, trace);
	
	JUIFilter.attribute(jFilter, attribute);
	find psmToUiTrace(attribute, componentType, _);
}

//JPackage Queries
pattern JPackageToUIModuleQuery(jPackage : JPackage) {
	find notOnlyContainerPackage(jPackage);
	find findUIRelevantPackages(jPackage);
	find hasUuid(jPackage, _);
	JPackage.name(jPackage, _);	
	neg find alreadyTransformed(jPackage, _, _);
}

pattern JPackageToUIModuleQueryForModify(jPackage : JPackage, trace : PSMToUITrace) {
	find psmToUiTrace(jPackage, _, trace);
}

private pattern findUIRelevantPackages(jPackage : JPackage) {
	find findPackagesInLayer(jPackage, JLayer::SCREEN);
} or {
	find findPackagesInLayer(jPackage, JLayer::OPERATION);
} or {
	find findPackagesInLayer(jPackage, JLayer::ALL);
}

private pattern findPackagesInLayer(jPackage : JPackage, layer : JLayer) {
	JPackage.participates(jPackage, layer);
}

private pattern notOnlyContainerPackage(jPackage : JPackage) {
	primitives == count JPackage.primitives(jPackage, _);
	enumerations == count JPackage.enumerations(jPackage, _);
	relationships == count JPackage.relationships(jPackage, _);
	classes == count JPackage.classes(jPackage, _);
	check (
		enumerations + primitives + relationships + classes > 0
	);
}

//UIClass Queries
pattern findDescendantsForClass(uiClass : UIClass, descendant : UIClass) {
	UIClass.^super+(descendant, uiClass);
}

pattern findDescendantsForClassWithJClass(uiClass : UIClass, descendant : UIClass, jClass : JClass) {
	UIClass.^super+(descendant, uiClass);
	find alreadyTransformed(jClass, descendant, _);
}


//JClass Queries
pattern JClassWithGuardConditionQuery(jClass : JClass, uiModule : UIModule) {
	find hasUuid(jClass, _);
	JClass.^package(jClass, jPackage);
	find findUIRelevantPackages(jPackage);
	neg find alreadyTransformed(jClass, _, _);
	find alreadyTransformed(jPackage, uiModule, _);
}

pattern JClassWithGuardConditionQueryForModify(jClass : JClass, trace : PSMToUITrace) {
	find psmToUiTrace(jClass, _, trace);
}

pattern representsUserJClassQuery(jClass : JClass) {
	JClass.representsUser(jClass, true);
}


//JOperation Queries
pattern JOperationWithGuardQuery(jOperation : JOperation, uiClass : UIClass) {
	find hasUuid(jOperation, _);
	JOperation.visibility(jOperation, JVisibility::PUBLIC);
	JOperation.ownerClass(jOperation, jClass);
	find alreadyTransformed(jClass, uiClass, _);
	neg find alreadyTransformed(jOperation, _, _);
}

pattern JOperationWithGuardQueryForModify(jOperation : JOperation, uiClass : UIClass, trace : PSMToUITrace) {
	find psmToUiTrace(jOperation, _, trace);
	JOperation.ownerClass(jOperation, jClass);
	find psmToUiTrace(jClass, uiClass, _);
}

//JParameter Queries
pattern JParameterWithGuardQuery(jParameter : JParameter, uiAction : UIAction, uiType : Identifiable) {
	find hasUuid(jParameter, _);
	JOperation.parameters(jOperation, jParameter);
	JOperation.visibility(jOperation, JVisibility::PUBLIC);
	find alreadyTransformed(jOperation, uiAction, _);
	JParameter.type(jParameter, type);
	find alreadyTransformed(type, uiType, _);
	neg find alreadyTransformed(jParameter, _, _);
}

pattern JParameterWithGuardQueryForModify(jParameter : JParameter, uiAction : UIAction, uiType : Identifiable, trace : PSMToUITrace) {
	JOperation.parameters(jOperation, jParameter);
	find psmToUiTrace(jOperation, uiAction, _);
	find psmToUiTrace(jParameter, _, trace);
	JParameter.type(jParameter, type);
	find psmToUiTrace(type, uiType, _);
}

pattern JParameterViewFieldQuery(jParameter : JParameter, parameterType : UIParameterComponentType, uiAction : UIAction) {
	find psmToUiTrace(jParameter, parameterType, _);
	JOperation.parameters(jOperation, jParameter);
	find psmToUiTrace(jOperation, uiAction, _);
}

//JUIMenuItem Queries
pattern findMenuItemWithParent(jMenuItem : JUIMenuItem) {
	find hasUuid(jMenuItem, _);
	JUIMenuItem.parent(jMenuItem, _);
	neg find alreadyTransformed(jMenuItem, _, _);
}

pattern findMenuItemToSetParent(jMenuItem : JUIMenuItem, uiMenuItem : UIMenuItem) {
	find psmToUiTrace(jMenuItem, uiMenuItem, _);
}

pattern findMenuItemWithParentForModify(jMenuItem : JUIMenuItem, trace : PSMToUITrace) {
	find psmToUiTrace(jMenuItem, _, trace);
}

//JRole Queries
pattern JRoleQuery(jRole : JRole, opposite : JRole, uiClass : UIClass) {
	find hasUuid(jRole, _);
	find hasUuid(opposite, _);
	JRelationship.roles(relation, jRole);
	JRelationship.roles(relation, opposite);
	jRole != opposite;
	neg find alreadyTransformed(jRole, _, _);
	JRole.ownerClass(jRole, jClass);
	find alreadyTransformed(jClass, uiClass, _);
}

pattern JRoleQueryForModify(jRole : JRole, uiClass : UIClass, trace : PSMToUITrace) {
	find psmToUiTrace(jRole, _, trace);
	JRole.ownerClass(jRole, jClass);
	find psmToUiTrace(jClass, uiClass, _);
}

pattern findOppositeForRole(jRole : JRole, oppositeComponentType : UIReferenceComponentType) {
	JRelationship.roles(relation, jRole);
	JRelationship.roles(relation, opposite);
	jRole != opposite;

	find psmToUiTrace(opposite, oppositeComponentType, _);
}

pattern JRoleViewFieldQuery(jRole : JRole, uiClass : UIClass, referenceComponentType : UIReferenceComponentType) {
	find psmToUiTrace(jRole, referenceComponentType, _);
	JRole.ownerClass(jRole, jClass);
	JRole.navigable(jRole, true);
	neg JRole.visibility(jRole, JVisibility::PRIVATE);
	find psmToUiTrace(jClass, uiClass, _);
}


//JAttribute Queries
pattern JAttributeQuery(jAttribute : JAttribute, uiClass : UIClass, baseType : UIBaseComponentType) {
	find hasUuid(jAttribute, _);
	JClass.attributes(jClass, jAttribute);
	JAttribute.type(jAttribute, jPrimitive);
	find alreadyTransformed(jPrimitive, baseType, _);
	find alreadyTransformed(jClass, uiClass, _);
	neg find alreadyTransformed(jAttribute, _, _);
}

pattern JAttributeQueryForModify(jAttribute : JAttribute, uiClass : UIClass, baseType : UIBaseComponentType, trace : PSMToUITrace) {
	find psmToUiTrace(jAttribute, _, trace);
	JAttribute.ownerClass(jAttribute, jClass);
	JAttribute.type(jAttribute, jPrimitive);
	find psmToUiTrace(jPrimitive, baseType, _);
	find psmToUiTrace(jClass, uiClass, _);
}

pattern JAttributeInJGroupViewFieldQuery(jAttribute : JAttribute, viewFieldSet : UIViewFieldSet, uiBaseType : UIBaseComponentType, classView : UIClassView) {
	find psmToUiTrace(jAttribute, uiBaseType, _);
	JUIAttributeGroup.attributes(jGroup, jAttribute);
	find psmToUiTrace(jGroup, viewFieldSet, _);
	JClass.attributeOrder(jClass, jGroup);
	find psmToUiTrace(jClass, uiClass, _);
	UIClass.classView(uiClass, classView);
}

pattern JAttributeViewFieldQueryForModify(jAttribute : JAttribute, uiClass : UIClass, uiBaseType : UIBaseComponentType) {
	find psmToUiTrace(jAttribute, uiBaseType, _);
	UIBaseComponentType.^private(uiBaseType, false);
	JAttribute.ownerClass(jAttribute, jClass);
	find psmToUiTrace(jClass, uiClass, _);
}

pattern findViewFieldForComponentType(componentType : UIComponentType, uiView : UIView, viewField : UIViewField, viewFieldSet : UIViewFieldSet) {
	UIView.viewFieldSets(uiView, viewFieldSet);
	UIViewFieldSet.viewFields(viewFieldSet, viewField);
	UIViewField.componentType(viewField, componentType);
}

//JUIAttributeGroup Queries
pattern JAttributeGroupQuery(jAttributeGroup : JUIAttributeGroup, classView : UIClassView) {
	find hasUuid(jAttributeGroup, _);
	JClass.attributeOrder(jClass, jAttributeGroup);
	find psmToUiTrace(jClass, uiClass, _);
	UIClass.classView(uiClass, classView);
	neg find alreadyTransformed(jAttributeGroup, _, _);
}

pattern JAttributeGroupQueryForModify(jAttributeGroup : JUIAttributeGroup, classView : UIClassView, trace : PSMToUITrace) {
	JClass.attributeOrder(jClass, jAttributeGroup);
	find alreadyTransformed(jClass, uiClass, _);
	UIClass.classView(uiClass, classView);
	find psmToUiTrace(jAttributeGroup, _, trace);
}


//Trace Queries
pattern psmToUiTrace(jElement : JElement, identifiable : Identifiable, trace : PSMToUITrace) {
	PSMToUITrace.psmElements(trace, jElement);
	PSMToUITrace.uiElements(trace, identifiable);
}

pattern alreadyTransformed(jElement : JElement, identifiable : Identifiable, trace : PSMToUITrace) {
	PSMToUITrace.psmElements(trace, jElement);
	PSMToUITrace.uiElements(trace, identifiable);
	Identifiable.uuid(identifiable, uuid);
	check (
		uuid.length > 0
	);
}

//PSMToUIMapping Queries
pattern findCorrespondingType(jPrimitive : JPrimitive, componentType : UIBaseComponentType) {
	Mapping.componentType(mapping, componentType);
	Mapping.psmType(mapping, jPrimitive);
	neg find alreadyTransformed(jPrimitive, componentType, _);
}



