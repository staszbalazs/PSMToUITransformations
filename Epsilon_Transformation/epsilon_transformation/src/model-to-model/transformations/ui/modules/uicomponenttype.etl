import "../../../operations/_importAll.eol";

rule UIReferenceComponentType
	transform jrole : PSM!JRole
	to uireference : UI!UIReferenceComponentType {
		//inherited from Identifiable
		uireference.name = jrole.name;
		//attributes
		if (jrole.visibility = PSM!JVisibility#PROTECTED) {
			uireference.readonly = true;
		} else if (jrole.visibility = PSM!JVisibility#PRIVATE) {
			uireference.private = true;
		}
		if (jrole.ownerRelationship.derived or jrole.calculated or jrole.visibility = PSM!JVisibility#PROTECTED) {
			uireference.value = "";
		} else {
			uireference.value = jrole.value;
		}
		uireference.derived = jrole.ownerRelationship.derived;
		uireference.lower = jrole.lower;
		uireference.upper = jrole.upper;
		uireference.navigable = jrole.navigable;
		uireference.kind = jrole.kind.name.toLowerCase();
		uireference.interval = jrole.interval;
		
		var uiClassUuid : String = jrole.ownerClass.equivalent("UIClass").uuid;
		uireference.uuid = generateUuid(uiClassUuid, uireference.name, ".");
			
		uireference.opposite = jrole.opposite().equivalent("UIReferenceComponentType");
		uireference.referenced = jrole.opposite().ownerClass.equivalent("UIClass");
		uireference.type = uireference.referenced.name;
		
		uireference.createIntervals(jrole.interval, uiClassUuid);
	}


rule UIBaseComponentType
	transform jattribute : PSM!JAttribute
	to uibasetype : UI!UIBaseComponentType {
		//inherited from Identifiable
		uibasetype.name = jattribute.name;
		//attributes
		if (jattribute.visibility = PSM!JVisibility#PROTECTED) {
			uibasetype.readonly = true;
		} else if (jattribute.visibility = PSM!JVisibility#PRIVATE) {
			uibasetype.private = true;
		}
		if (jattribute.derived or jattribute.calculated or jattribute.visibility = PSM!JVisibility#PROTECTED) {
			uibasetype.value = "";
		} else {
			uibasetype.value = jattribute.value;
		}
		uibasetype.derived = jattribute.derived;
		uibasetype.lower = jattribute.lower;
		uibasetype.upper = jattribute.upper;
		uibasetype.regexp = jattribute.regexp;
		uibasetype.decimals = jattribute.decimals;
		uibasetype.placeholder = jattribute.placeholder;		
		uibasetype.type = jattribute.type.componentType().name;
		uibasetype.interval = jattribute.interval;
		
		var uiClassUuid : String = jattribute.ownerClass.equivalent("UIClass").uuid;
		uibasetype.uuid = generateUuid(uiClassUuid, uibasetype.name, ".");
		uibasetype.createIntervals(jattribute.interval, uiClassUuid);
		
		uibasetype.~search = not jattribute.uiNoSearch;
	} 


rule UIParameterComponentType
	transform jparameter : PSM!JParameter
	to uiparameter : UI!UIParameterComponentType {
	guard : jparameter.ownerOperation.isDefined() and jparameter.ownerOperation.visibility = PSM!JVisibility#PUBLIC
		//inherited from Identifiable
		uiparameter.uuid = jparameter.uuid;
		uiparameter.name = jparameter.name;
		//attributes
		uiparameter.value = "";
		uiparameter.lower = jparameter.lower;
		uiparameter.upper = jparameter.upper;
		uiparameter.interval = jparameter.interval;
		
		if (jparameter.type.isTypeOf(PSM!JClass)) {
			uiparameter.referenced = jparameter.type.equivalent("UIClass");
			uiparameter.type = jparameter.type.equivalent("UIClass").name;
		} else {
			uiparameter.type = jparameter.type.componentType().name;
		}
		
		uiparameter.createIntervals(jparameter.interval, jparameter.ownerOperation.ownerClass.uuid);
	}
	
	
